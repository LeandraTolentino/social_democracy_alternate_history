title: Building a People's Republic
new-page: true
is-card: true
card-image: img/poster_0.jpg
view-if: neorevisionism > 0 and peoples_party_support >= 3 and peoples_party = 0 and peoples_party_timer == 0
on-arrival: peoples_party_timer = 6
tags: party_affairs
max-visits: 4
#subtitle: Can the IRSP move beyond the working class?

= Building a People's Republic

Contrary to orthodox Marxist predictions, capitalism struggles to develop in the imperial periphery.

In order to build a socialist majority, many in our party believe that the outlook of the Communist Party of China ought to be adopted. Many believe we must represent not just the working class, but also the peasantry, the petit-bourgeoisie and national bourgeoisie.

- @support_peoples_party: Yes, we must become a people's republic!
- @build_support: We must build additional support before adopting Maoism.
- @support_peoples_party_hard: Despite strong opposition from the Neo-trotskyists and Labor, we will adopt Maoism.
- @no_new_ideas: No, we will always be a party of the working class.
- @root: Perhaps we can discuss this at a later time.
- @easy_discard
- @cancel_advisor_action

@build_support
view-if: (neorevisionist_strength + reformist_strength < left_strength + center_strength) or peoples_party_support < 5
on-arrival: peoples_party_support += 1; neorevisionist_strength += 5; left_strength -= 5; left_dissent += 5

Within our party, we begin purging and side-lining the Communist Left and Labor movement.

@support_peoples_party
choose-if: (neorevisionist_strength + reformist_strength >= left_strength + center_strength) and peoples_party_support >= 4
unavailable-subtitle: The opposition is too strong.
on-arrival: neorevisionist_strength += 15; reformist_strength += 15; neorevisionist_dissent -= 10; reformist_dissent -= 10; peoples_party = 1; workers_spd -= 12; rural_spd += 10; new_middle_spd += 10; old_middle_spd += 8; catholics_spd += 8; left_dissent += 25; center_dissent += 20; kpd_relation -= 10; kpd_coalition_dissent += 1; coalition_dissent -= 1; z_relation += 5*(1-dissent); dvp_relation += 5*(1-dissent)
achievement: volkspartei

In an emergency conference, we have decided to work towards a people's republic that encompasses all of those classes opposed to imperialism, not just the urban poor. Our party must represent also the peasantry, the petit-bourgeoisie and the national bourgeoisie. The Communist left claims that our new party platform is a betrayal of our founding principles, a turn away from Marxism towards bourgeois opportunism. Whatever the case, we are now a party representing all of the people, not just the proletariat.

Should we run a new campaign to publicize our new platform?

- @campaign_workers
- @campaign_middle
- @campaign_rural
- @campaign_unemployed
- @campaign_women



@support_peoples_party_hard
subtitle: This will increase dissent.
view-if: (neorevisionist_strength + reformist_strength < left_strength + center_strength) or (peoples_party_support < 4)
on-arrival: neorevisionist_strength += 10; reformist_strength += 8; peoples_party = 1; workers_spd -= 15; rural_spd += 10; new_middle_spd += 10; old_middle_spd += 8; catholics_spd += 8; left_dissent += 40; center_dissent += 40; kpd_relation -= 10; kpd_coalition_dissent += 1; coalition_dissent -= 1
achievement: volkspartei

It is an arduous and contentious process, but we have pushed through the proposal to become a people's republic. The Communist Left claims that our new party platform is a betrayal of our founding principles, a turn away from Marxism towards bourgeois opportunism. Whatever the case, we are now a party representing all of the people, not just the proletariat.

Should we run a new campaign to publicize our new platform?

- @campaign_workers
- @campaign_middle
- @campaign_rural
- @campaign_unemployed
- @campaign_women

@campaign_workers
title: Campaign among the workers, to assuage their doubts about our changes.
on-arrival: workers_spd += 6*(1-dissent);

The workers are still a key part of our constituency, and we must not alienate them in the transformation of our party.

@campaign_middle
title: Campaign among the bourgeoisie.
on-arrival: old_middle_spd += 5*(1-dissent); new_middle_spd += 6*(1-dissent)

As a Maoist party, we represent also the petit-bourgeoisie and the national bourgeoisie, and they are becoming receptive to our messages.

@campaign_rural
title: Campaign in rural areas.
choose-if: rural_policy > 0
unavailable-subtitle: We have not enacted any rural policies yet.
on-arrival: rural_spd += 7*(1-dissent)

As a Maoist party, we represent poor farmers and rural workers as well, and they are becoming receptive to our messages.

@campaign_unemployed
title: Campaign among the unemployed.
on-arrival: unemployed_spd += 6*(1-dissent);

As a Maoist party, the urban poor are still a key part of our constituency.

@campaign_women
title: Campaign among women of the four classes.
on-arrival: workers_spd += 3*(1-dissent); new_middle_spd += 3*(1-dissent); old_middle_spd += 3*(1-dissent); rural_spd += 3*(1-dissent); unemployed_spd += 3*(1-dissent);

As a Maoist party, we represent women of the four social classes.


@no_new_ideas
on-arrival: neorevisionist_strength -= 6; neorevisionist_dissent += 5; center_strength += 8; left_strength += 3; peoples_party = 0; peoples_party_support -= 3

The IRSP have been and will always be a party of the working class.

